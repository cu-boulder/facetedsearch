PREFIX rdf:   <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:  <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd:   <http://www.w3.org/2001/XMLSchema#>
PREFIX owl:   <http://www.w3.org/2002/07/owl#>
PREFIX vitro: <http://vitro.mannlib.cornell.edu/ns/vitro/0.7#>
PREFIX dco: <http://info.deepcarbon.net/schema#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX vitro-public: <http://vitro.mannlib.cornell.edu/ns/vitro/public#>
PREFIX vivo: <http://vivoweb.org/ontology/core#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX bibo: <http://purl.org/ontology/bibo/>
PREFIX pubs: <https://experts.colorado.edu/ontology/pubs#>

CONSTRUCT
{
    ?publication a bibo:Document .
    ?publication vitro:mostSpecificType ?mostSpecificType .
    ?mostSpecificType rdfs:label ?typeLabel .
    ?publication rdf:type ?mostSpecificType .
    ?publication rdfs:label ?label .
    ?publication bibo:doi ?doi .
    ?publication pubs:cuscholar ?cuscholar .
    ?publication bibo:abstract ?abstract .
    ?publication vivo:relatedBy ?authorship .
    ?authorship rdf:type vivo:Authorship .
    ?authorship vivo:relates ?author .
    ?publication vivo:dateTimeValue ?pubdateobj .
    ?pubdateobj vivo:dateTime ?pubdatevalue .
    ?publication vivo:hasPublicationVenue ?venue .
    ?venue rdfs:label ?venueLabel .
    ?publication bibo:pageEnd ?pageend .
    ?publication bibo:pageStart ?pagestart .
    ?publication bibo:issue ?issue .
    ?publication bibo:numPages ?numpages .
    ?publication bibo:volume ?volume .
    ?publication pubs:citedAuthors ?authors .
    ?publication bibo:isbn13 ?isbn13 .
    ?publication bibo:isbn10 ?isbn10 .
    ?publication bibo:issn ?issn .
    ?publication bibo:eissn ?eissn .
    ?publication bibo:number ?articlenumber .
    ?publication pubs:dataSource ?datasource .
    ?publication pubs:authorCount ?authorcount .
    ?publication pubs:citationCount ?citationcount .
    ?publication pubs:dateInCube ?dateincube .
    ?publication pubs:fundingAcknowledgement ?fundingacknowledgement .
    ?publication bibo:presentedAt ?conference .
    ?conference rdf:type bibo:Conference .
    ?conference rdfs:label ?nameofconference .
}
WHERE
{
   ?publication a bibo:Document .
   ?publication vitro:mostSpecificType ?mostSpecificType .
   ?mostSpecificType rdfs:label ?typeLabel .
   ?publication rdfs:label ?label .
   ?publication vivo:relatedBy ?authorship .
   ?authorship rdf:type vivo:Authorship .
   ?authorship vivo:relates ?author .
   OPTIONAL { ?publication bibo:doi ?doi }
   OPTIONAL { ?publication pubs:cuscholar ?cuscholar }
   OPTIONAL { ?publication bibo:abstract ?abstract }
   OPTIONAL { ?publication bibo:pageEnd ?pageend }
     OPTIONAL { ?publication bibo:pageStart ?pagestart }
     OPTIONAL { ?publication bibo:issue ?issue }
     OPTIONAL { ?publication bibo:numPages ?numpages }
       OPTIONAL { ?publication bibo:volume ?volume }
       OPTIONAL { ?publication pubs:citedAuthors ?authors  }
   OPTIONAL { ?publication vivo:dateTimeValue ?pubdateobj .
              ?pubdateobj vivo:dateTime ?pubdatevalue
            }
   OPTIONAL { ?publication vivo:hasPublicationVenue ?venue .
              ?venue rdfs:label ?venueLabel
            }
 OPTIONAL { ?publication bibo:isbn13 ?isbn13 }
 OPTIONAL { ?publication bibo:isbn10 ?isbn10 }
 OPTIONAL { ?publication bibo:number ?articlenumber }
 OPTIONAL { ?publication bibo:issn ?issn }
 OPTIONAL { ?publication bibo:eissn ?eissn }
 OPTIONAL { ?publication pubs:dataSource ?datasource }
 OPTIONAL { ?publication pubs:authorCount ?authorcount }
 OPTIONAL { ?publication pubs:citationCount ?citationcount }
 OPTIONAL { ?publication pubs:dateInCube ?dateincube }
 OPTIONAL { ?publication pubs:fundingAcknowledgement ?fundingacknowledgement }
 OPTIONAL { ?publication bibo:presentedAt ?conference .
            ?conference rdf:type bibo:Conference .
            ?conference rdfs:label ?nameofconference .
 }
}

